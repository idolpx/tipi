; Data used by routines in powerup

VDPSTACK	EQU	>8370

fbufheader
	DATA	>AA3F,>FF11
	BYTE	>03
	EVEN

; Power UP routine to reset TIPI latches.
onreset				;
	STWP	R5		; Hold WP for EQU based offsets.

	CLR	@TDOUT		; Clear TIPI data output
	CLR	@TCOUT		; Clear control output

; If the PI is alive, trigger it to dirty RCIN
	LI  R1,>F100   ; this is the brief reset echo for
	                ; synchronizing the ack counter
	MOVB R1,@TCOUT  ; send that to the PI.
	LI	R2,>1000	; setup a limited loop
!	MOVB @RCIN,R1   ; read the ack register from TIPI
	CI	R1,>F100    ;
	JEQ crureset    ; escape out of the limited retry loop
	DEC R2
	JNE -!			; if we leave hear early, RCIN will be 0xF1
					; and later we get to check that full reset
					; happened by RCIN becoming 0x00

crureset
; Before resetting, clear TCOUT again, or the handshake on receive messsage
; will begin prematurely.
	CLR	@TCOUT

; Trigger reset signal to RPi
; This kills the TipiService and restarts it asynchronously.
	SBO	1		; turn on the second cru bit
	LI	R1,>2000
!	DEC	R1
	JNE	-!

; turn off the reset signal so RPi service can finish starting
	SBZ	1

; setup disk buffers if crubase is that of floppy controller
	CI	R12,>1100
	JNE	tipizero
	LI	R2,>37D7	; room for 3 file buffers
	MOV	R2,@VDPSTACK
	LI	R1,VDPWA
	INC	R2		; compose vdp write address
	ORI	R2,VDWRITE
	MOVB	@R2LB(R5),*R1
	MOVB	R2,*R1		; write address for vdp is set
	CLR	R2
fheadcopy
	MOVB	@fbufheader(R2),@VDPWD
	INC	R2
	CI	R2,5
	JNE	fheadcopy

; Wait for TIPI to zero out RCIN when TipiService starts.
	LI	R3,>FFFF
	LI	R2,>FFFF
tipizero
	CLR	R1
	CB	@RCIN, R1
	JEQ done
	DEC	R2
	JNE	tipizero
	DEC	R3
	JNE	tipizero

; Return to TI startup
done
	RT			; return to console

